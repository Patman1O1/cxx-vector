#-----------------------------------------------------------------------------------------------------------------------
# Information
#-----------------------------------------------------------------------------------------------------------------------
# Project Information
cmake_minimum_required(VERSION @CMAKE_MINIMUM_REQUIRED_VERSION@)
project("@PROJECT_NAME@"
        VERSION @PROJECT_VERSION@
        DESCRIPTION "@PROJECT_DESCRIPTION@"
        LANGUAGES CXX
)
set(PROJECT_NAMESPACE @PROJECT_NAMESPACE@ CACHE STRING "The namespace the project is in")
set(PROJECT_OUTPUT_NAME @PROJECT_OUTPUT_NAME@ CACHE STRING "The base name that will be displayed on project output files")
set(PROJECT_PACKAGE_NAME @PROJECT_PACKAGE_NAME@ CACHE STRING "The name that will be displayed on CMake package related files")
set(PROJECT_PRIMARY_TARGET @PROJECT_PRIMARY_TARGET@)

# CMake Information
cmake_file_api(
        QUERY
        API_VERSION 1
        CODEMODEL 2.3
)

#-----------------------------------------------------------------------------------------------------------------------
# Settings
#-----------------------------------------------------------------------------------------------------------------------
# C++ Settings
set(CMAKE_CXX_STANDARD @CMAKE_CXX_STANDARD@)
set(CMAKE_CXX_STANDARD_REQUIRED @CMAKE_CXX_STANDARD_REQUIRED@)

# Project Settings
option(BUILD_SHARED_LIBS "Build the project as a shared library" @BUILD_SHARED_LIBS@)
option(BUILD_TESTS "Build the project's tests" OFF)

# Set default build type if not specified
if(NOT DEFINED CMAKE_BUILD_TYPE AND NOT DEFINED CMAKE_CONFIGURATION_TYPES)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Build type" FORCE)
    set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "Debug" "Release" "MinSizeRel" "RelWithDebInfo")
endif()

#-----------------------------------------------------------------------------------------------------------------------
# CCache
#-----------------------------------------------------------------------------------------------------------------------
find_program(CCACHE_PROGRAM ccache)
if(CCACHE_PROGRAM)
    message(STATUS "CCache found")
    set(CMAKE_CXX_COMPILER_LAUNCHER "${CCACHE_PROGRAM}")
else()
    message(STATUS "CCache not found")
endif()

#-----------------------------------------------------------------------------------------------------------------------
# Interprocedural Optimisation (IPO)
#-----------------------------------------------------------------------------------------------------------------------
include(CheckIPOSupported)
check_ipo_supported(RESULT result OUTPUT output)
if(result)
    message(STATUS "IPO is available")
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
else()
    message(SEND_ERROR "IPO is unavailable: ${output}")
endif()

#-----------------------------------------------------------------------------------------------------------------------
# CPack
#-----------------------------------------------------------------------------------------------------------------------
include(CPack)

# Package setup
set(CPACK_PACKAGE_NAME ${PROJECT_NAME})
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${PROJECT_DESCRIPTION}")
set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})
set(CPACK_PACKAGE_INSTALL_DIRECTORY "${PROJECT_NAME}")
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/LICENSE")
set(CPACK_RESOURCE_FILE_README "${CMAKE_SOURCE_DIR}/README.md")
set(CPACK_SOURCE_IGNORE_FILES
        /.git
        /.vscode
        /.*build.*
        /\\\\.gitignore
        /\\\\.DS_Store
)

# Component setup
set(CPACK_COMPONENTS_ALL Runtime Configuration Documentation)
set(CPACK_COMPONENT_RUNTIME_DISPLAY_NAME "${PROJECT_NAME}")
set(CPACK_COMPONENT_RUNTIME_DESCRIPTION "${PROJECT_DESCRIPTION}")
set(CPACK_COMPONENT_CONFIGURATION_DISPLAY_NAME "${PROJECT_NAME}")
set(CPACK_COMPONENT_CONFIGURATION_DESCRIPTION "${PROJECT_DESCRIPTION}")
set(CPACK_COMPONENT_DOCUMENTATION_DISPLAY_NAME "Documentation")
set(CPACK_COMPONENT_DOCUMENTATION_DESCRIPTION "${PROJECT_DESCRIPTION}")

#-----------------------------------------------------------------------------------------------------------------------
# Uninstall
#-----------------------------------------------------------------------------------------------------------------------
add_custom_target(uninstall
    COMMAND ${CMAKE_COMMAND} -P ${CMAKE_SOURCE_DIR}/cmake/cmake_uninstall.cmake
)

#-----------------------------------------------------------------------------------------------------------------------
# Subdirectories
#-----------------------------------------------------------------------------------------------------------------------
add_subdirectory("src")

if(BUILD_TESTS)
    add_subdirectory("test")
endif()

